[doc Demo the loop statement]

[global file=loop_break.tmp]

###
### Vanilla style of loops
###
[shell vanilla]
    # Iterate over a fix list of items
    [loop color green yellow red]
        !echo "Color $color"
        ?SH-PROMPT:
    [endloop]

    # Iterate over a range
    [loop i 1..5]
        !echo "Iter $i"
        ?SH-PROMPT:
    [endloop]

    # Iterate over a mix of strings, reverse range and range with custom incr
    [local colors=green yellow red]
    [local incr=3]
    [loop mix $color 7..4 3..9..$incr]
        !echo "Mix $mix"
        ?SH-PROMPT:
    [endloop]

###
### Loop with break patterns
###
[shell background]
    # Setup a background job which eventually adds DONE to a file
    !echo foo > $file;
    ?SH-PROMPT:
    !(sleep 3; echo DONE >> $file)

[shell foreground]
    # Probe the file for the DONE string using a break pattern
    [loop i 1..7]
        @DONE
        [progress i=$i]
        !cat $file
        ?SH-PROMPT:
        [sleep 1]
    [endloop]
    ?SH-PROMPT:

[cleanup]
    !rm -f $file
    ?SH-PROMPT:
